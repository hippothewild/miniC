package Example;

import java_cup.runtime.*;

action code {:
:}

parser code {:
	public static void main(String args[]) throws Exception {
		new parser(new Yylex(System.in)).parse();
	}
:}

/* Parenthesis and Brackets. */
terminal LPAREN, RPAREN, LBRACKET, RBRACKET, LSQBRACKET, RSQBRACKET;

/* Operators and Assigner. */
terminal PLUS, MINUS, TIMES, DIVIDE, ASSIGN, UNOP;

/* Comparer. */
terminal LESS, GREATER, LESS_EQ, GREATER_EQ, EQ, NOT_EQ;

/* Tokens. */
terminal TOKEN_INT, TOKEN_FLOAT, TOKEN_RETURN, TOKEN_WHILE, TOKEN_DO, TOKEN_FOR, TOKEN_IF, TOKEN_ELSE, TOKEN_CASE, TOKEN_SWITCH, TOKEN_BREAK, TOKEN_DEFAULT;

/* Identifier. */
terminal String ID;

/* Types. */
terminal Float		FLOAT_NUM;
terminal Integer	INT_NUM;

/* Miscellaneous. */
terminal SEMI, COLON, COMMA;

/* Non-terminal program rules. */
non terminal program;
non terminal declList;
non terminal funcList;
non terminal declaration;
non terminal identList;
non terminal identifier;
non terminal function;
non terminal paramList;
non terminal type;
non terminal compoundStmt;
non terminal stmtList;
non terminal stmt;
non terminal assignStmt;
non terminal assign;
non terminal callStmt;
non terminal call;
non terminal retStmt;
non terminal whileStmt;
non terminal forStmt;
non terminal ifStmt;
non terminal switchStmt;
non terminal caseList;
non terminal caseStmt;
non terminal expr;
non terminal argList;
/*
non terminal Program		program;
non terminal DeclList		declList;
non terminal FuncList		funcList;
non terminal Declaration	declaration;
non terminal IdentList		identList;
non terminal Identifier		identifier;
non terminal Function		function;
non terminal ParamList		paramList;
non terminal Type           type;
non terminal CompoundStmt	compoundStmt;
non terminal StmtList		stmtList;
non terminal Stmt			stmt;
non terminal AssignStmt		assignStmt;
non terminal Assign			assign;
non terminal CallStmt		callStmt;
non terminal Call			call;
non terminal RetStmt		retStmt;
non terminal WhileStmt		whileStmt;
non terminal ForStmt		forStmt;
non terminal IfStmt			ifStmt;
non terminal SwitchStmt		switchStmt;
non terminal CaseList		caseList;
non terminal CaseStmt		caseStmt;
non terminal Expr			expr;
non terminal ArgList		argList;
*/

precedence right ASSIGN;
precedence left EQ, NOT_EQ;
precedence left LESS, GREATER, LESS_EQ, GREATER_EQ;
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE;
precedence right UNOP;
precedence left LPAREN, RPAREN;

/* Program grammars. */

program	::=
		  | declList
		  | funcList
		  | declList funcList
		{:System.out.println("[program]");:}
		  ;

declList ::= declaration
		   | declList declaration
		{:System.out.println("[declList]");:}
		   ;

funcList ::= function
		   | funcList function
		{:System.out.println("[funcList]");:}
		   ;

declaration ::= type identList
		{:System.out.println("[declaration]");:}
			  ;

identList ::= identifier
			| identList COMMA identifier
		{:System.out.println("[identList]");:}
			;

identifier ::= ID
			 | ID LSQBRACKET INT_NUM RSQBRACKET
		{:System.out.println("[identifier]");:}
			 ;

function ::= type ID LPAREN RPAREN compoundStmt
		   | type ID LPAREN paramList RPAREN compoundStmt
		{:System.out.println("[function]");:}
		   ;

paramList ::= type identifier
			| paramList COMMA type identifier
		{:System.out.println("[paramList]");:}
			;

type ::= TOKEN_INT
	   | TOKEN_FLOAT
	{:System.out.println("[type]");:}
	   ;

compoundStmt ::= LBRACKET stmtList RBRACKET
			   | LBRACKET declList stmtList RBRACKET
			{:System.out.println("[compoundStmt]");:}
			   ;

stmtList ::=
		   | stmtList stmt
		{:System.out.println("[stmtList]");:}
		   ;

stmt ::= assignStmt
	   | callStmt
	   | retStmt
       | whileStmt
	   | forStmt
	   | ifStmt
	   | switchStmt
	   | compoundStmt
	   | SEMI
	{:System.out.println("[stmt]");:}
	   ;

assignStmt ::= assign SEMI
	{:System.out.println("[assignStmt]");:}
			 ;

assign ::= ID ASSIGN expr
		 | ID LSQBRACKET expr RSQBRACKET ASSIGN expr
		{:System.out.println("[assign]");:}
		 ;

callStmt ::= call SEMI
		{:System.out.println("[callStmt]");:}
		   ;

call ::= ID LPAREN RPAREN
	   | ID LPAREN argList RPAREN
	{:System.out.println("[call]");:}
       ;

retStmt ::= TOKEN_RETURN SEMI
		  | TOKEN_RETURN expr SEMI
		  ;


whileStmt ::= TOKEN_WHILE LPAREN expr RPAREN stmt
			| TOKEN_DO stmt TOKEN_WHILE LPAREN expr RPAREN SEMI
			;

forStmt ::= TOKEN_FOR LPAREN assign SEMI expr SEMI assign RPAREN stmt
		  ;

ifStmt ::= TOKEN_IF LPAREN expr RPAREN stmt
		 | TOKEN_IF LPAREN expr RPAREN stmt TOKEN_ELSE stmt
		{:System.out.println("[ifStmt]");:}
		 ;

switchStmt ::= TOKEN_SWITCH LPAREN identifier RPAREN LBRACKET caseList RBRACKET
			 ;

caseList ::= caseStmt
		   | caseStmt TOKEN_DEFAULT COLON stmtList
		   | caseStmt TOKEN_DEFAULT COLON stmtList TOKEN_BREAK SEMI
		   ;

caseStmt ::=
		   | caseStmt TOKEN_CASE INT_NUM COLON stmtList
		   | caseStmt TOKEN_CASE INT_NUM COLON stmtList TOKEN_BREAK SEMI
		   ;

expr ::= UNOP expr
	   | expr TIMES expr
	   | expr DIVIDE expr
	   | expr PLUS expr
	   | expr MINUS expr
	   | expr LESS expr
	   | expr GREATER expr
	   | expr LESS_EQ expr
	   | expr GREATER_EQ expr
	   | expr EQ expr
	   | expr NOT_EQ expr
	   | call
	   | INT_NUM
	   | FLOAT_NUM
	   | ID
	   | ID LSQBRACKET expr RSQBRACKET
	   | LPAREN expr RPAREN
	{:System.out.println("[expr]");:}
	   ;

argList ::= expr
		  | argList COMMA expr
		  ;
